buildscript {
    repositories {
        mavenCentral()
        maven { url "https://repo.grails.org/grails/core" }
        maven { url "https://plugins.gradle.org/m2/" }
        maven { url 'https://repo.gradle.org/gradle/libs-releases' }
        if (project.projectVersion.endsWith('-SNAPSHOT')) {
            maven { url "https://s01.oss.sonatype.org/content/repositories/snapshots/" }
        }
    }
    dependencies {
        classpath "io.github.gradle-nexus:publish-plugin:1.3.0"
        classpath "org.graceframework:grace-gradle-plugin:$graceVersion"
        classpath "org.gradle:test-retry-gradle-plugin:1.4.0"
        classpath "io.spring.gradle:dependency-management-plugin:$springDependencyManagementPluginVersion"
    }
}

ext {
    isReleaseVersion = !project.projectVersion.endsWith('-SNAPSHOT')
    graceVersion = project.graceVersion
    gradleWrapperVersion = project.gradleWrapperVersion
    userOrg = "graceframework"
}

version project.projectVersion

apply plugin:'idea'

apply plugin: 'maven-publish'
apply plugin: "io.github.gradle-nexus.publish-plugin"

nexusPublishing {
    repositories {
        sonatype {
            def ossUser = System.getenv("SONATYPE_USERNAME") ?: project.hasProperty("sonatypeOssUsername") ? project.sonatypeOssUsername : ''
            def ossPass = System.getenv("SONATYPE_PASSWORD") ?: project.hasProperty("sonatypeOssPassword") ? project.sonatypeOssPassword : ''
            def ossStagingProfileId = System.getenv("SONATYPE_STAGING_PROFILE_ID") ?: project.hasProperty("sonatypeOssStagingProfileId") ? project.sonatypeOssStagingProfileId : ''
            nexusUrl = uri("https://s01.oss.sonatype.org/service/local/")
            snapshotRepositoryUrl = uri("https://s01.oss.sonatype.org/content/repositories/snapshots/")
            username = ossUser
            password = ossPass
            stagingProfileId = ossStagingProfileId
        }
    }
}

subprojects { project->
    version project.projectVersion

    configurations.all {
        resolutionStrategy.eachDependency { DependencyResolveDetails details ->
            if(details.requested.group == 'org.codehaus.groovy') {
                details.useVersion(groovyVersion)
            }
        }
    }

    repositories {
        mavenCentral()
        maven { url "https://repo.grails.org/grails/core" }
        if (project.projectVersion.endsWith('-SNAPSHOT')) {
            maven { url "https://s01.oss.sonatype.org/content/repositories/snapshots/" }
        }
    }

    if (project.name.startsWith('examples') || project.name.endsWith('docs')) {
        return
    }

    ext.isGrailsPlugin = project.name in ['views-component', 'views-markup', 'views-json']

    apply plugin: 'eclipse'
    apply plugin: 'idea'
    apply plugin: 'java-library'
    apply plugin: 'groovy'
    apply plugin: 'maven-publish'
    apply plugin: 'signing'
    apply plugin: "org.gradle.test-retry"
    apply plugin: "io.spring.dependency-management"

    if (ext.isGrailsPlugin) {
        apply plugin: "org.graceframework.grace-plugin"
    }

    java {
        toolchain {
            languageVersion = JavaLanguageVersion.of(17)
        }
        withJavadocJar()
        withSourcesJar()
    }

    task wrapper(type: Wrapper) {
        gradleVersion = gradleWrapperVersion
    }

    apply from: '../publishing/mavenCentral.gradle'


    if (project.name in ["views-core", "views-component", "views-json", "views-markup", "views-json-testing-support"]) {

        configurations {
            documentation
        }

        dependencies {
            documentation "org.fusesource.jansi:jansi:$jansiVersion"
            documentation "org.codehaus.groovy:groovy-dateutil:$groovyVersion"
            documentation "info.picocli:picocli:4.6.3"
            documentation "com.github.javaparser:javaparser-core:$javaParserCoreVersion"

            testImplementation "org.spockframework:spock-core:$spockVersion"
            testImplementation "org.codehaus.groovy:groovy-test-junit5:${groovyVersion}"
            testImplementation "org.junit.jupiter:junit-jupiter-api:5.8.2"
            testImplementation "org.junit.platform:junit-platform-runner:1.8.2"
            testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:5.8.2"
        }

        java {
            withJavadocJar()
            withSourcesJar()
        }

        tasks.withType(Test) {
            useJUnitPlatform()
            testLogging {
                showStandardStreams = true
                exceptionFormat = 'full'
            }
            configure {
                retry {
                    maxRetries = 2
                    maxFailures = 20
                    failOnPassedAfterRetry = true
                }
            }
        }

        groovydoc.classpath = configurations.documentation
    }

}
