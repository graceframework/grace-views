buildscript {
    repositories {
        maven { url "https://repo.grails.org/grails/core" }
        maven { url "https://plugins.gradle.org/m2/" }
    }
    dependencies {
        classpath "io.github.gradle-nexus:publish-plugin:1.0.0"
        classpath "org.grails:grails-gradle-plugin:$grailsVersion"
    }
}

ext {
    isReleaseVersion = !version.endsWith('-SNAPSHOT')
    grailsVersion = project.grailsVersion
    gradleWrapperVersion = project.gradleWrapperVersion
    userOrg = "grails"
}

apply plugin:'idea'

if (isReleaseVersion) {
    apply plugin: 'maven-publish'
    apply plugin: "io.github.gradle-nexus.publish-plugin"

    nexusPublishing {
        repositories {
            sonatype {
                def ossUser = System.getenv("SONATYPE_USERNAME") ?: project.hasProperty("sonatypeOssUsername") ? project.sonatypeOssUsername : ''
                def ossPass = System.getenv("SONATYPE_PASSWORD") ?: project.hasProperty("sonatypeOssPassword") ? project.sonatypeOssPassword : ''
                def ossStagingProfileId = System.getenv("SONATYPE_STAGING_PROFILE_ID") ?: project.hasProperty("sonatypeOssStagingProfileId") ? project.sonatypeOssStagingProfileId : ''
                username = ossUser
                password = ossPass
                stagingProfileId = ossStagingProfileId
            }
        }
    }
}

subprojects { subProject->

    if (subProject.name.startsWith('examples') || subProject.name.endsWith('docs')) {
        return
    }

    ext.isGrailsPlugin = subProject.name in ['views-markup', 'views-json']

    apply plugin: 'eclipse'
    apply plugin: 'idea'
    apply plugin: 'groovy'
    apply plugin: 'maven-publish'
    apply plugin: 'signing'

    if (ext.isGrailsPlugin) {
        apply plugin: "org.grails.grails-plugin"
    }

    version project.projectVersion

    sourceCompatibility = 1.8
    targetCompatibility = 1.8

    repositories {
        mavenCentral()
        maven { url "https://repo.grails.org/grails/core" }
    }

    task wrapper(type: Wrapper) {
        gradleVersion = gradleWrapperVersion
    }

    apply from: '../publishing/grailsCentralPublishing.gradle'

}
