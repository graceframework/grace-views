import org.grails.gradle.plugin.web.gsp.GroovyPageCompileTask

buildscript {
    repositories {
        mavenCentral()
        maven { url "https://repo.grails.org/grails/core" }
        maven { url 'https://repo.gradle.org/gradle/libs-releases' }
    }
    dependencies {
        classpath "org.graceframework:grace-gradle-plugin:$graceVersion"
        classpath "io.github.gradle-nexus:publish-plugin:1.3.0"
        classpath "io.spring.gradle:dependency-management-plugin:$springDependencyManagementPluginVersion"
    }
}

group "org.graceframework.plugins"

apply plugin: "org.graceframework.grace-gsp"
apply plugin: "io.spring.dependency-management"

dependencyManagement {
    imports {
        mavenBom "org.graceframework:grace-bom:$graceVersion"
    }
    applyMavenExclusions false
}

dependencies {
    api "org.graceframework:grace-core:$graceVersion"
    compileOnly "org.graceframework:grace-boot:$graceVersion"
    api project(":views-core")

    testImplementation "org.graceframework:grace-test-support:$graceVersion"
    testImplementation "org.graceframework:grace-datastore-gorm-hibernate5"
    testRuntimeOnly "org.javassist:javassist:3.29.0-GA"

    console "org.graceframework:grace-console"
}

tasks.withType(GroovyPageCompileTask) {
    classpath += configurations.compileClasspath
}

jar {
  exclude "*.gson"
}

springBoot {
    mainClass.set('grails.plugin.json.view.Application')
}
