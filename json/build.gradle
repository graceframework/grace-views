import org.grails.gradle.plugin.web.gsp.GroovyPageCompileTask

buildscript {
    repositories {
        maven { url "https://repo.grails.org/grails/core" }
    }
    dependencies {
        classpath "org.grails:grails-gradle-plugin:$grailsVersion"
    }
}

plugins {
    id 'eclipse'
    id 'idea'
    id "io.spring.dependency-management" version "1.0.6.RELEASE"
}

group "org.grails.plugins"

apply plugin: "org.grails.grails-plugin"
apply plugin: "org.grails.grails-gsp"
apply plugin: "org.grails.grails-plugin-publish"
apply plugin: 'java-library'

sourceCompatibility = 1.8
targetCompatibility = 1.8

configurations.all {
    resolutionStrategy.eachDependency { DependencyResolveDetails details ->
        if(details.requested.group == 'org.grails' &&
                details.requested.name.startsWith('grails-datastore')) {

            if (details.requested.name.equals("grails-datastore-gorm-hibernate5")) {
                details.useVersion("$hibernateGormVersion")
            } else if (details.requested.name.startsWith("grails-datastore-gorm-mongodb") ||
                    details.requested.name.equals("grails-datastore-gorm-bson")) {
                details.useVersion("$mongodbVersion")
            } else {
                details.useVersion("$gormVersion")
            }
        }

    }
    resolutionStrategy.cacheChangingModulesFor 0, 'seconds'
}

dependencies {
    api "org.grails:grails-core:$grailsVersion"
    api project(":views-core")

    testImplementation "org.grails:grails-web-testing-support:$testingSupportVersion"
    testImplementation "org.grails:grails-gorm-testing-support:$testingSupportVersion"
    testImplementation "org.grails:grails-datastore-gorm-hibernate5"
    testRuntimeOnly "org.javassist:javassist:3.18.1-GA"

    console "org.grails:grails-console"
}

tasks.withType(GroovyPageCompileTask) {
    classpath += configurations.compileClasspath
}

task wrapper(type: Wrapper) {
    gradleVersion = gradleWrapperVersion
}

jar {
  exclude "*.gson"
}

grailsPublish {
    userOrg = 'grails'
    githubSlug = 'grails/grails-views'
    license {
        name = 'Apache-2.0'
    }
    title = "JSON Views Plugin"
    desc = "Adds support for defining views in JSON"
    developers = [graemerocher:"Graeme Rocher"]
}

bintray {
    pkg {
        desc = "Grails Views"
        name = 'grails-views'
    }
}

springBoot {
    mainClass.set('grails.plugin.json.view.Application')
}
